import QtQuick
import QtQuick.Layouts
import Quickshell
import Quickshell.Io
import Quickshell.Widgets
import qs.config
import qs.modules.widgets
import qs.services
import qs.plugins

ContentMenu {
    title: "Appearance"
    description: "Adjust how the desktop looks like."

    ContentCard {
        ColumnLayout {
            Layout.fillWidth: true
            spacing: 16

            // Theme Selection
            ColumnLayout {
                spacing: 4

                StyledText {
                    text: "Select Theme"
                    font.pixelSize: 16
                }

                StyledText {
                    text: "Choose between dark or light mode."
                    font.pixelSize: 12
                    color: "#888888"
                }
            }

            RowLayout {
                Layout.leftMargin: 15
                Layout.fillWidth: true
                Layout.alignment: Qt.AlignHCenter
                spacing: 16

                // Dark Theme Button
                StyledButton {
                    Layout.preferredHeight: 300
                    Layout.preferredWidth: 460
                    Layout.maximumHeight: 400
                    Layout.maximumWidth: 500
                    icon: "dark_mode"
                    iconSize: 64
                    checked: Config.runtime.appearance.theme === "dark"
                    hoverEnabled: true

                    onClicked: {
                        if (!Config.runtime.appearance.colors.autogenerated) {
                            const scheme = Config.runtime.appearance.colors.scheme
                            const file = Theme.map[scheme]?.dark
                            if (!file) {
                                Theme.notifyMissingVariant(scheme, "dark")
                                return
                            }

                            Config.updateKey("appearance.theme", "dark")
                            Quickshell.execDetached([
                                "bash",
                                Directories.scriptsPath + "/interface/switchTheme.sh",
                                file
                            ])
                        } else {
                            Config.updateKey("appearance.theme", "dark")
                            Quickshell.execDetached([
                                "qs", "-c", "nucleus-shell", "ipc", "call", "global", "regenColors"
                            ])
                        }
                    }
                }

                // Light Theme Button
                StyledButton {
                    Layout.preferredHeight: 300
                    Layout.preferredWidth: 460
                    Layout.maximumHeight: 400
                    Layout.maximumWidth: 500
                    icon: "light_mode"
                    iconSize: 64
                    checked: Config.runtime.appearance.theme === "light"
                    hoverEnabled: true

                    onClicked: {
                        if (!Config.runtime.appearance.colors.autogenerated) {
                            const scheme = Config.runtime.appearance.colors.scheme
                            const file = Theme.map[scheme]?.light
                            if (!file) {
                                Theme.notifyMissingVariant(scheme, "light")
                                return
                            }

                            Config.updateKey("appearance.theme", "light")
                            Quickshell.execDetached([
                                "bash",
                                Directories.scriptsPath + "/interface/switchTheme.sh",
                                file
                            ])
                        } else {
                            Config.updateKey("appearance.theme", "light")
                            Quickshell.execDetached([
                                "qs", "-c", "nucleus-shell", "ipc", "call", "global", "regenColors"
                            ])
                        }
                    }
                }
            }

            Item {
                width: 30
            }
        }
    }

    // Color Generation Schemes
    ContentCard {
        RowLayout {
            opacity: autogeneratedColorsSelector.enabled ? 1 : 0.8
            ColumnLayout {
                StyledText {
                    text: "Color Generation Schemes:"
                    font.pixelSize: 16
                }

                StyledText {
                    text: "Choose the scheme for autogenerated color generation."
                    font.pixelSize: 12
                }
            }

            Item { Layout.fillWidth: true }

            StyledDropDown {
                id: autogeneratedColorsSelector
                label: "Color Scheme"
                model: [
                    "scheme-content",
                    "scheme-expressive",
                    "scheme-fidelity",
                    "scheme-fruit-salad",
                    "scheme-monochrome",
                    "scheme-neutral",
                    "scheme-rainbow",
                    "scheme-tonal-spot"
                ]

                currentIndex: model.indexOf(Config.runtime.appearance.colors.matugenScheme)

                onSelectedIndexChanged: (index) => {
                    if (!Config.runtime.appearance.colors.autogenerated) 
                        return
                    const selectedScheme = model[index]
                    Config.updateKey("appearance.colors.matugenScheme", selectedScheme)
                    Quickshell.execDetached([
                        "qs", "-c", "nucleus-shell", "ipc", "call", "global", "regenColors"
                    ])
                }

                enabled: Config.runtime.appearance.colors.autogenerated
            }
        }

        // Predefined/Custom Themes
        RowLayout {
            opacity: predefinedThemeSelector.enabled ? 1 : 0.8
            ColumnLayout {
                StyledText {
                    font.pixelSize: 16
                    text: "Predefined/Custom Themes:"
                }

                StyledText {
                    font.pixelSize: 12
                    text: "Choose a pre-defined theme for your interface."
                }
            }

            Item { Layout.fillWidth: true }

            StyledDropDown {
                id: predefinedThemeSelector
                label: "Theme"
                model: Object.keys(Theme.map)
                currentIndex: model.indexOf(Config.runtime.appearance.colors.scheme)

                onSelectedIndexChanged: (index) => {
                    if (Config.runtime.appearance.colors.autogenerated) 
                        return
                    const selectedTheme = model[index]
                    const variant = Config.runtime.appearance.theme
                    const file = Theme.map[selectedTheme][variant]
                    if (!file) return

                    Config.updateKey("appearance.colors.scheme", selectedTheme)
                    Quickshell.execDetached([
                        "bash",
                        Directories.scriptsPath + "/interface/switchTheme.sh",
                        file
                    ])
                }

                enabled: !Config.runtime.appearance.colors.autogenerated
            }
        }
    }

    // Icon Tint & Autogenerated Themes
    ContentCard {
        StyledSwitchOption {
            title: "Tint Icons"
            description: "Either tint icons across the shell or keep them colorized."
            prefField: "appearance.tintIcons"
        }

        StyledSwitchOption {
            title: "Use Autogenerated Themes"
            description: "Use autogenerated themes."
            prefField: "appearance.colors.autogenerated"
        }

    }

    // Clock Settings
    ContentCard {
        StyledText {
            text: "Clock"
            font.pixelSize: 20
            font.bold: true
        }

        StyledSwitchOption {
            title: "Show Clock"
            description: "Whether to show or disable the clock on the background."
            prefField: "appearance.background.clock.enabled"
        }

        StyledSwitchOption {
            title: "Analog Variant"
            description: "Whether to use analog clock or not."
            prefField: "appearance.background.clock.isAnalog"
        }

        RowLayout {
            id: shapeSelector

            ColumnLayout {
                StyledText {
                    text: "Analog Clock Shape"
                    font.pixelSize: 16
                }

                StyledText {
                    text: "Choose the analog clock's shape."
                    font.pixelSize: 12
                }
            }

            Item { Layout.fillWidth: true }


            StyledDropDown {
                label: "Shape Type"
                model: ["Cookie 7 Sided", "Cookie 9 Sided", "Cookie 12 Sided", "Pixelated Circle", "Circle"]

                currentIndex: Config.runtime.appearance.background.clock.shape

                onSelectedIndexChanged: (index) => {
                    Config.updateKey(
                        "appearance.background.clock.shape",
                        index
                    )
                }
            }
        }
    }

    ContentCard {
        StyledText {
            text: "Rounding"
            font.pixelSize: 20
            font.bold: true
        }
        NumberStepper {
            label: "Factor"
            description: "Adjust the rounding factor."
            prefField: "appearance.rounding.factor"
            minimum: 0
            maximum: 1
            step: 0.1
        }
    }
    ContentCard {
        StyledText {
            text: "Animations"
            font.pixelSize: 20
            font.bold: true
        }
        StyledSwitchOption {
            title: "Enabled"
            description: "Whether to enable or disable animations (applies everywhere in the shell)."
            prefField: "appearance.animations.enabled"
        }
    }
}
