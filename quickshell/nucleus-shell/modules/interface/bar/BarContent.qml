import QtQuick
import QtQuick.Layouts
import Quickshell
import "content/"
import qs.config
import qs.modules.components

Item {
    property bool isHorizontal: (Config.runtime.bar.position === "top" || Config.runtime.bar.position === "bottom")

    Row {
        id: hCenterRow

        visible: isHorizontal
        anchors.centerIn: parent
        spacing: Metrics.spacing(4)

        SystemUsageModule { }
        MediaPlayerModule { }
        WorkspaceModule { }
        ClockModule { }
        BatteryIndicatorModule { }
    }

    RowLayout {
        id: hLeftRow

        visible: isHorizontal
        anchors.left: parent.left
        anchors.verticalCenter: parent.verticalCenter
        spacing: Metrics.spacing(4)
        anchors.leftMargin: Config.runtime.bar.density * 0.3

        
        StyledText {
            id: hGlyph
            Layout.alignment: Qt.AlignLeft
            Layout.rightMargin: Metrics.margin("small") - 4
            font.pixelSize: Metrics.fontSize("huge") * 1.7
            color: Globals.visiblility.sidebarLeft ? Appearance.m3colors.m3error : Appearance.syntaxHighlightingTheme
            text: "✦"

            MouseArea {
                id: ma
                anchors.fill: parent
                onClicked: Globals.visiblility.sidebarLeft = !Globals.visiblility.sidebarLeft
            }
        }
        

        ActiveWindowModule { }
    }

    RowLayout {
        id: hRightRow

        visible: isHorizontal
        anchors.right: parent.right
        anchors.verticalCenter: parent.verticalCenter
        spacing: Metrics.spacing(4)
        anchors.rightMargin: Config.runtime.bar.density * 0.3

        SystemTray { id: sysTray }

        StyledText {
            visible: (sysTray.items.count > 0) && Config.runtime.bar.modules.statusIcons.enabled
            id: seperator
            Layout.alignment: Qt.AlignLeft
            font.pixelSize: Metrics.fontSize("hugeass")
            text: "·"
        }

        StatusIconsModule { }

    }

    // Vertical Layout
    Item {
        visible: !isHorizontal
        anchors.top: parent.top
        anchors.topMargin: Config.runtime.bar.density * 0.1
        anchors.horizontalCenter: parent.horizontalCenter

        implicitWidth: vRow.implicitHeight
        implicitHeight: vRow.implicitWidth

        Row {
            id: vRow

            anchors.centerIn: parent
            spacing: Metrics.spacing(8)
            rotation: 90

            
            ToggleModule {
                icon: "menu"
                iconSize: Metrics.iconSize(22)
                iconColor: Appearance.m3colors.m3error
                toggle: Globals.visiblility.sidebarLeft
                rotation: 270
                onToggled: Globals.visiblility.sidebarLeft = value
            }
            

            SystemUsageModule { }
            MediaPlayerModule { }
            SystemTray { rotation: 0 }

        }
    }

    Item {
        visible: !isHorizontal
        anchors.centerIn: parent
        anchors.verticalCenterOffset: 35
        implicitWidth: centerRow.implicitHeight
        implicitHeight: centerRow.implicitWidth

        Row {
            id: centerRow

            anchors.centerIn: parent

            WorkspaceModule {
                rotation: 90
            }
        }
    }

    Item {
        visible: !isHorizontal
        anchors.bottom: parent.bottom
        anchors.bottomMargin: Config.runtime.bar.density * 0.1
        anchors.horizontalCenter: parent.horizontalCenter
        implicitWidth: row.implicitHeight
        implicitHeight: row.implicitWidth

        Row {
            id: row

            anchors.centerIn: parent
            spacing: Metrics.spacing(6)
            rotation: 90


            ClockModule {
                rotation: 270
            }

            StatusIconsModule { }
            BatteryIndicatorModule { }

            
            ToggleModule {
                icon: "power_settings_new"
                iconSize: Metrics.iconSize(22)
                iconColor: Appearance.m3colors.m3error
                toggle: Globals.visiblility.powermenu
                rotation: 270
                onToggled: Globals.visiblility.powermenu = value
            }
            
        }
    }
}
