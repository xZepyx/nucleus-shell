pragma Singleton
pragma ComponentBehavior: Bound
import QtQuick
import QtCore
import Quickshell
import Quickshell.Io
import qs.plugins

Singleton {
    id: root
    property string filePath: Directories.shellConfigPath
    property alias runtime: configOptionsJsonAdapter
    property bool initialized: false
    property int readWriteDelay: 50
    property bool blockWrites: false

    function updateKey(nestedKey, value) {
        let keys = nestedKey.split(".")
        let obj = root.runtime
        if (!obj) {
            console.warn("Config.updateKey: adapter not available for key", nestedKey)
            return
        }

        for (let i = 0; i < keys.length - 1; ++i) {
            let k = keys[i]
            if (obj[k] === undefined || obj[k] === null || typeof obj[k] !== "object") {
                obj[k] = new JsonObject()  // Use JsonObject for serialization
            }
            obj = obj[k]
            if (!obj) {
                console.warn("Config.updateKey: failed to resolve", k)
                return
            }
        }

        let convertedValue = value
        if (typeof value === "string") {
            let trimmed = value.trim()
            if (trimmed === "true" || trimmed === "false" || (!isNaN(Number(trimmed)) && trimmed !== "")) {
                try {
                    convertedValue = JSON.parse(trimmed)
                } catch (e) {
                    convertedValue = value
                }
            }
        }

        obj[keys[keys.length - 1]] = convertedValue
        configFileView.adapterUpdated()
    }

    function loadPluginConfigs(plugins) {
        console.log("Loading plugins:", plugins)
        
        for (let i = 0; i < plugins.length; i++) {
            const name = plugins[i]
            const path = Directories.shellConfig + "/plugins/" + name + "/PluginConfigData.qml"

            const component = Qt.createComponent(path)
            if (component.status === Component.Error) {
                console.warn("Plugin failed:", path, component.errorString())
                continue
            }

            if (component.status === Component.Ready) {
                const pluginObj = component.createObject(root)
                if (!pluginObj) {
                    console.warn("Failed to create plugin object:", name)
                    continue
                }

                if (!pluginObj.defaults) pluginObj.defaults = { enabled: false }
                
                root.updateKey("plugins." + name, pluginObj.defaults)
                console.log("Loaded plugin:", name, pluginObj.defaults)
                
                pluginObj.destroy()
                component.destroy()
            }
        }
        console.log("Plugins loaded into runtime")
    }

    Timer { id: fileReloadTimer; interval: root.readWriteDelay; repeat: false; onTriggered: configFileView.reload() }
    Timer { id: fileWriteTimer; interval: root.readWriteDelay; repeat: false; onTriggered: configFileView.writeAdapter() }

    Timer {
        interval: 1200
        running: true
        repeat: false
        onTriggered: {
            console.log("Plugin timer triggered")
            root.loadPluginConfigs(PluginLoader.plugins)
        }
    }

    FileView {
        id: configFileView
        path: root.filePath
        watchChanges: true
        blockWrites: root.blockWrites
        onFileChanged: fileReloadTimer.restart()
        onAdapterUpdated: fileWriteTimer.restart()
        onLoaded: { root.initialized = true }
        onLoadFailed: error => {
            if (error == FileViewError.FileNotFound) writeAdapter()
        }

        JsonAdapter {
            id: configOptionsJsonAdapter
            
            property var plugins: ({}) // dynamic plugins config variable

            property JsonObject appearance: JsonObject {
                property string theme: "dark"
                property bool tintIcons: false
                property JsonObject animations: JsonObject { property bool enabled: true }
                property JsonObject rounding: JsonObject { property double factor: 0.6 }
                property JsonObject colors: JsonObject {
                    property string scheme: "catppuccin-lavender"
                    property string matugenScheme: "scheme-neutral"
                    property bool autogenerated: true
                    property bool runMatugenUserWide: false
                }
                property JsonObject background: JsonObject {
                    property bool enabled: true 
                    property url path: Directories.defaultsPath + "/default.jpg"
                    property JsonObject clock: JsonObject {
                        property bool enabled: true 
                        property bool isAnalog: true
                        property int edgeSpacing: 50
                        property int shape: 1
                        property int xPos: 0
                        property int yPos: 0
                    }
                    property JsonObject slideshow: JsonObject {
                        property bool enabled: false
                        property bool includeSubfolders: true
                        property int interval: 5
                        property string folder: ""
                    }
                }
            }

            property JsonObject misc: JsonObject { property url pfp: Quickshell.env("HOME") + "/.face.icon" }
            property JsonObject notifications: JsonObject {
                property bool enabled: true 
                property bool doNotDisturb: false
                property string position: "center"
            }
            property JsonObject shell: JsonObject {
                property string version: "0.2.0"
                property string releaseChannel: "stable"
            }
            property JsonObject overlays: JsonObject {
                property bool enabled: true 
                property bool volumeOverlayEnabled: true 
                property bool brightnessOverlayEnabled: true
                property string volumeOverlayPosition: "top"
                property string brightnessOverlayPosition: "top"
            }
            property JsonObject launcher: JsonObject {
                property bool fuzzySearchEnabled: true
                property string webSearchEngine: "google"
            }
            property JsonObject bar: JsonObject {
                property string position: "top"
                property bool enabled: true
                property bool merged: false
                property bool floating: false
                property bool gothCorners: true
                property int radius: Appearance.rounding.large
                property int margins: Appearance.margin.normal
                property int density: 50
                property JsonObject modules: JsonObject {
                    property int radius: Appearance.rounding.normal
                    property int height: 34
                    property JsonObject workspaces: JsonObject {
                        property bool enabled: true
                        property int workspaceIndicators: 8
                        property bool showAppIcons: true 
                        property bool showJapaneseNumbers: false
                    }
                    property JsonObject statusIcons: JsonObject {
                        property bool enabled: true
                        property bool networkStatusEnabled: true 
                        property bool bluetoothStatusEnabled: true 
                        property bool keyboardLayoutStatusEnabled: false
                        property bool themeStatusEnabled: true
                    }
                    property JsonObject systemUsage: JsonObject {
                        property bool enabled: true
                        property bool cpuStatsEnabled: true 
                        property bool memoryStatsEnabled: true 
                        property bool tempStatsEnabled: true
                    }
                }
            }
        }
    }
}
